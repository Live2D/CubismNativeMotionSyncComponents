plugins {
    id 'com.android.application'
}

// SDK root path
def sdkRootPath = file("${projectDir}/../../../../../..").canonicalFile
// Search for NATIVE_DIR
def nativeDir = file("${sdkRootPath}/../CubismNativeSamples")
if (!nativeDir.exists()) {
    def tmpDirs = file("${sdkRootPath}/..").listFiles().findAll { File f ->
        f.directory && f.name.startsWith("CubismSdkForNative") && !f.name.endsWith(".zip")
    }
    if (!tmpDirs.isEmpty()) {
        nativeDir = tmpDirs[0]
    } else {
        nativeDir = null
    }
}

android {
    namespace = "com.live2d.demo"

    compileSdk PROP_COMPILE_SDK_VERSION.toInteger()

    defaultConfig {
        applicationId "com.live2d.demo"
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            ndk {
                abiFilters = []
                abiFilters.addAll(PROP_APP_ABI.split(':').collect{ it as String })
            }
            cmake {
                arguments "-DNATIVE_DIR=${nativeDir}", "-DSDK_ROOT_PATH=${sdkRootPath}"
            }
        }
        ndkVersion "26.3.11579264"
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    flavorDimensions = ["mode"]

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.22.1"
        }
    }
    sourceSets.main {
        assets.srcDir "../../../../../Resources"
        assets.srcDir "${nativeDir}/Samples/OpenGL/Shaders/StandardES"
        jniLibs.srcDirs "../../../../../../Core/CRI/dll/Android"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
}
